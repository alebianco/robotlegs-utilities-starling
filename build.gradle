/*
 * Project: robotlegs-extension-starling
 *
 * Author:  Alessandro Bianco
 * Website: http://alessandrobianco.eu
 * Twitter: @alebianco
 * Created: 18/10/2014 14.54
 */

buildscript {

    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.0'
    }

    defaultTasks 'build'

    files('gradle/user.properties', 'gradle/template.properties').each {
        File file -> file.withInputStream { stream ->
            Properties props = new Properties()
            props.load(stream)
            props.each { key, value -> project.properties.ext.setProperty(key, value) }
        }
    }

    project.properties.ext['product.version'] += '-SNAPSHOT'
}

apply plugin: org.gradlefx.plugins.GradleFxPlugin

type = 'swc'
fatSwc = true

flexHome = this.properties['sdk.flex.home']

additionalCompilerOptions = [
        '-swf-version=26',
        '-target-player=15.0',
        '-verbose-stacktraces=true',
        '-load-config=air-config.xml',
        '-static-link-runtime-shared-libraries=true',
        "-define=PRODUCT::name,'${this.properties['product.name']}'",
        "-define=PRODUCT::version,'${this.properties['product.version']}'",
]

dependencies {
    merged files("libs/robotlegs-framework-v2.2.1.swc",
                 "libs/starling-v1.5.1.swc",
                 "libs/feathers-v2.0.0.swc")

    test files("libs/flexUnitTasks-4.2.0-20140410.jar",
               "libs/flexunit-4.2.0-20140410-flex_4.12.0.swc",
               "libs/flexunit-uilistener-4.2.0-20140410-4.12.0.swc",
               "libs/flexunit-cilistener-4.2.0-20140410-4.12.0.swc")
}

flexUnit {
    command = this.properties['flash.player']
}

asdoc {
    additionalASDocOptions = [
            '-strict=false',
            '-lenient=false',
            '-left-frameset-width=320',
            "-window-title='${this.properties['product.name']} ${this.properties['product.version']}'",
            "-main-title='${this.properties['product.name']} ${this.properties['product.version']}'",
            "-footer='${this.properties['product.name']} - '${this.properties['repository.url']}'"
    ]
}

task release (dependsOn: ['clean', 'build'], type: Zip) {
    destinationDir new File(projectDir, 'release')
    archiveName "${this.properties['product.name']}-${this.properties['product.version']}_" + new Date().format('yyyyMMddHHmm') + ".zip"
    from('build') {
        include "${project.name}.swc"
        into 'bin'
    }
    from('doc') {
        include '**/*'
        exclude 'tempdita'
        into 'doc'
}
    from 'CHANGELOG.md'
    from 'CONTRIBUTING.md'
    from 'LICENSE.md'
    from 'README.md'
    rename { String fileName ->
        fileName.replace('.md', '.txt').toLowerCase()
    }
}

build.shouldRunAfter clean

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}